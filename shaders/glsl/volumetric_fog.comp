#version 460
#extension GL_GOOGLE_include_directive : enable
/*
    The MIT License (MIT)

    Copyright (c) 2014 bartwronski

    Permission is hereby granted, free of charge, to any person obtaining a copy
    of this software and associated documentation files (the "Software"), to deal
    in the Software without restriction, including without limitation the rights
    to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
    copies of the Software, and to permit persons to whom the Software is
    furnished to do so, subject to the following conditions:

    The above copyright notice and this permission notice shall be included in all
    copies or substantial portions of the Software.

    THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
    IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
    FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
    AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
    LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
    OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
    SOFTWARE.
*/
#include "util.h"

layout(local_size_x=4, local_size_y=4, local_size_z=4) in;

struct DirLight
{
    vec3 direction;
    vec3 color;
};

layout(rgba32f, set = 0, binding = 0) uniform writeonly image3D volumeOut;
layout(set = 0, binding = 1) uniform sampler3D volumeIn;

layout(set = 0, binding = 2) uniform VolumeGenerationInputs
{
    uvec3 resolution;
    vec3 cameraPos;
    vec3 cameraXAxis;
    vec3 cameraYAxis;
    vec3 cameraZAxis;
    float zNear;
    float zFar;
    mat4 prevFrameVP;
    DirLight dirLight;
    float time;
    int frameNumber;
    float historyFactor;
};

layout(set = 0, binding = 3) uniform DirShadower
{
    mat4 lightPV;
};

layout(set = 0, binding = 4) uniform sampler2D shadowMap;

#include "shadow_map.h"
#include "volumetric_fog_util.h"

float calculateDensityFunction(vec3 worldSpacePos)
{
    // some procedural effect
    // return turbulence(worldSpacePos * 0.35f + vec3(time * 0.1f, 0, 0), 2) * exp(-worldSpacePos.y * 0.5f);
    return clamp(exp(-worldSpacePos.y * 0.01), 0.0, 1.0) * 1.5;
}

// some generated Poisson-like distribution
const uint SAMPLE_NUM = 8;
const vec2 POISSON_SAMPLES[SAMPLE_NUM] =
{
    vec2(0.228132254148f, 0.67232428631f),
    vec2(0.848556554824f, 0.135723477704f),
    vec2(0.74820789575f, 0.63965073852f),
    vec2(0.472544801767f, 0.351474129111f),
    vec2(0.962881642535f, 0.387342871273f),
    vec2(0.0875977149838f, 0.896250211998f),
    vec2(0.203231652569f, 0.12436704431f),
    vec2(0.56452806916f, 0.974024350484f),
};

vec3 getSunLightingRadiance(vec3 worldPosition, vec3 viewDir, float anisotropy)
{
    float shadow = getShadowValue(lightPV, shadowMap, vec4(worldPosition, 1.0f), 0.001f);
    float sunPhaseFunction = getPhaseFunction(dot(dirLight.direction, viewDir), anisotropy);
    return shadow * dirLight.color * sunPhaseFunction;
}

vec3 getAmbient(vec3 worldPosition, vec3 viewDir, float anisotropy)
{
//    vec3 volumeTexPosition = (worldPosition - g_WorldBoundsMin.xyz) * g_WorldBoundsInvRange.xyz * GI_VOLUME_TEX_SCALE + GI_VOLUME_TEX_BIAS;
//    vec4 shRed = giVolumeTexR.SampleLevel(linearSampler, volumeTexPosition, 0);
//    vec4 shGreen = giVolumeTexG.SampleLevel(linearSampler, volumeTexPosition, 0);
//    vec4 shBlue = giVolumeTexB.SampleLevel(linearSampler, volumeTexPosition, 0);
//    vec4 shPhaseFunction = GetRotatedPhaseFunctionSH(viewDir, anisotropy);
//
//    vec3 lighting = 0.0f;
//    lighting.r = max(dot(shPhaseFunction, shRed), 0);
//    lighting.g = max(dot(shPhaseFunction, shGreen), 0);
//    lighting.b = max(dot(shPhaseFunction, shBlue), 0);
//    
//    return lighting;

    return 0.001f * dirLight.color;
}



void main() 
{
    if(any(greaterThanEqual(gl_GlobalInvocationID, resolution))) {
        return;
    }
    // bool reprojectionOn = g_VolumetricFogReprojectionOn > 0.5f;
    bool reprojectionOn = historyFactor > 0.0f;
    
    vec2 currFrameJitter = reprojectionOn ? (POISSON_SAMPLES[(frameNumber + gl_GlobalInvocationID.x + gl_GlobalInvocationID.y * 2) % SAMPLE_NUM] - 0.5f) : vec2(0.0f);

    vec3 screenCoords = ndcFromThreadID(max(vec3(gl_GlobalInvocationID) + vec3(currFrameJitter, 0.0f), 0.0f), resolution);
    float linearDepth = volumeZPosToDepth(screenCoords.z);
    float layerThickness = volumeZPosToDepth(screenCoords.z + 1.0f / float(resolution.z)) - linearDepth;
    // Our scene is at a much greater scale with much more fog range, hence the units are off
    layerThickness *= 0.01f;

    vec3 worldSpacePos = worldPositionFromNdcCoords(screenCoords.xy, linearDepth);

    float dustDensity = calculateDensityFunction(worldSpacePos); //InputTexture[gl_GlobalInvocationID].x;
    // float scattering = g_VolumetricFogFinalScattering * (0.1f + dustDensity) * layerThickness;
    float scattering = (0.1f + dustDensity) * layerThickness;
    float absorption = 0.1f * layerThickness;
    float anisotropy = -0.3;
    vec3 fogAlbedo = vec3(0.8f, 0.8f, 0.7f);
    vec3 viewDir = normalize(worldSpacePos - cameraPos.xyz);

    vec3 lighting = vec3(0.0f);

    lighting += getSunLightingRadiance(worldSpacePos, viewDir, anisotropy);
    lighting += getAmbient(worldSpacePos, viewDir, anisotropy);
    // TODO: Add point light(s)
    //lighting += GetLocalLightsRadiance(worldSpacePos, viewDir, anisotropy);

    lighting *= fogAlbedo;

    vec4 finalOutValue = vec4(lighting * scattering, scattering + absorption);

    if(reprojectionOn) {
        vec3 ndcNoJitter = ndcFromThreadID(max(vec3(gl_GlobalInvocationID), 0.0f), resolution);
        float linearDepthNoJitter = volumeZPosToDepth(ndcNoJitter.z);
        vec3 worldSpacePosNoJitter = worldPositionFromNdcCoords(ndcNoJitter.xy, linearDepthNoJitter);
        vec4 prevFrameProjected = prevFrameVP * vec4(worldSpacePosNoJitter, 1.0);
        vec3 prevFrameNdc = prevFrameProjected.xyz / prevFrameProjected.w;
        float prevFrameLinearDepth = linearizeDepth(prevFrameNdc.z, zNear, zFar);
        vec3 uvw = vec3(prevFrameNdc.xy * 0.5f + 0.5f, depthToVolumeZPos(prevFrameLinearDepth));
        if(all(greaterThanEqual(uvw, vec3(0.0f))) && all(lessThanEqual(uvw, vec3(1.0f, 1.0f, 1.0f)))) {
            vec4 fogPrevFrame = texture(volumeIn, uvw);
            finalOutValue = mix(finalOutValue, fogPrevFrame, historyFactor);
        }
    }

    imageStore(volumeOut, ivec3(gl_GlobalInvocationID), finalOutValue);
}
